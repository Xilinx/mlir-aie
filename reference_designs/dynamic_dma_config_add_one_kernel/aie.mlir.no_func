//===- aie.mlir ------------------------------------------------*- MLIR -*-===//
//
// Copyright (C) 2020-2022, Xilinx Inc.
// Copyright (C) 2022, Advanced Micro Devices, Inc.
// SPDX-License-Identifier: MIT
//
//===----------------------------------------------------------------------===//

module {
  %t70 = aie.tile(6, 0)
  %t71 = aie.tile(6, 1)
  %t72 = aie.tile(6, 2)

  aie.flow(%t70, "DMA" : 0, %t72, "DMA" : 0)
  aie.flow(%t70, "DMA" : 1, %t72, "DMA" : 1)
  aie.flow(%t72, "DMA" : 0, %t70, "DMA" : 0)
  aie.flow(%t72, "DMA" : 1, %t70, "DMA" : 1)

  %buf72_0 = aie.buffer(%t72) { sym_name = "in" } : memref<16xi32>
  %buf72_1 = aie.buffer(%t72) { sym_name = "out" } : memref<16xi32>

  %l72_0 = aie.lock(%t72, 0)
  %l72_1 = aie.lock(%t72, 1)

  %m72 = aie.mem(%t72) {
      %srcDma = aie.dma_start(S2MM, 0, ^bd0, ^dma0)
    ^dma0:
      %dstDma = aie.dma_start(MM2S, 0, ^bd1, ^end)
    ^bd0:
      aie.use_lock(%l72_0, "Acquire", 0)
      aie.dma_bd(%buf72_0 : memref<16xi32>, 0, 16)
      aie.use_lock(%l72_0, "Release", 1)
      aie.next_bd ^bd1
    ^bd1:
      aie.use_lock(%l72_1, "Acquire", 1)
      aie.dma_bd(%buf72_1 : memref<16xi32>, 0, 16)
      aie.use_lock(%l72_1, "Release", 0)
      aie.next_bd ^bd0
    ^end:
      aie.end
  }

  aie.core(%t72) {
    %c16 = arith.constant 16 : index
    %c0 = arith.constant 0 : index
    %c1 = arith.constant 1 : index
    %c1_32 = arith.constant 1 : i32

    aie.use_lock(%l72_0, "Acquire", 1)
    aie.use_lock(%l72_1, "Acquire", 0)
    scf.for %arg3 = %c0 to %c16 step %c1 {
        %0 = memref.load %buf72_0[%arg3] : memref<16xi32>
        %1 = arith.addi %0, %c1_32 : i32
        memref.store %1, %buf72_1[%arg3] : memref<16xi32>
    }
    aie.use_lock(%l72_0, "Release", 0)
    aie.use_lock(%l72_1, "Release", 1)

    aie.end
  }

}
